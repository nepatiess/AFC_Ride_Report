CREATE PROCEDURE SP_GENERATEREPORT
@START_DATE DATETIME, 
@END_DATE DATETIME
AS
BEGIN
    SET NOCOUNT ON;
    DECLARE @SQL NVARCHAR(MAX);
    DECLARE @COLUMN_LIST NVARCHAR(MAX) = '';
    DECLARE @PIVOT_LIST NVARCHAR(MAX) = '';
    DECLARE @TOTAL_CALC NVARCHAR(MAX) = '';
    
	-- DÝNAMÝK KOLON LÝSTESÝ ÜRET
    SELECT 
        @COLUMN_LIST = STRING_AGG('ISNULL([' + CARD_TYPE + '],0) AS [' + CARD_TYPE + ']', ', '),
        @PIVOT_LIST  = STRING_AGG('[' + CARD_TYPE + ']', ', '),
        @TOTAL_CALC  = STRING_AGG('ISNULL([' + CARD_TYPE + '],0)', ' + ')
    FROM CARDTYPES 
    WHERE ISACTIVE = 1;
    
	-- DÝNAMÝK SQL OLUÞTUR
    SET @SQL = N' 
    ;WITH TRANSACTION_DATA AS (
        SELECT 
            CAST(T.TRANSACTION_DATE AS DATE) AS TRANSACTION_DATE,
            V.PLATE + '' ('' + V.VEHICLE_BRAND + '' '' + V.VEHICLE_MODEL + '')'' AS VEHICLE_INFO,
            CT.CARD_TYPE AS CARD_TYPE,
            SUM(T.SPENT_AMOUNT) AS TOTAL_AMOUNT
        FROM TRANSACTIONS T -- TRANSACTIONS

        INNER JOIN VEHICLE V ON T.VEHICLE_ID = V.VEHICLE_ID
        INNER JOIN CARDTYPES CT ON T.CARD_TYPE_ID = CT.CARD_TYPE_ID
        WHERE CAST(T.TRANSACTION_DATE AS DATE) BETWEEN ''' + CONVERT(VARCHAR(10), @START_DATE, 120) + ''' 
                                                   AND ''' + CONVERT(VARCHAR(10), @END_DATE, 120) + '''
        GROUP BY CAST(T.TRANSACTION_DATE AS DATE), V.PLATE, V.VEHICLE_BRAND, V.VEHICLE_MODEL, CT.CARD_TYPE
    )
    , PIVOTDATA AS (
        SELECT 
            TRANSACTION_DATE,
            VEHICLE_INFO,
            ' + @COLUMN_LIST + '
        FROM (
		-- BURAYA CARD_ID VE CARD_NO EKLEDÝM.
            SELECT TRANSACTION_DATE, VEHICLE_INFO, CARD_TYPE, TOTAL_AMOUNT
            FROM TRANSACTION_DATA
        ) AS SOURCE_TABLE
        PIVOT (
            SUM(TOTAL_AMOUNT) 
            FOR CARD_TYPE IN (' + @PIVOT_LIST + ')
        ) AS PVT
    )
    SELECT 
        TRANSACTION_DATE AS [DATE],
        VEHICLE_INFO AS [VEHICLE INFO],
        ' + @COLUMN_LIST + ',
        (' + @TOTAL_CALC + ') AS [SUMMARY]
    FROM PIVOTDATA
    ORDER BY TRANSACTION_DATE, VEHICLE_INFO;';
    

	EXEC sp_executesql @SQL;

END;



EXEC SP_GENERATEREPORT 
    @START_DATE = '2024-01-01', 
    @END_DATE = '2026-01-31'
